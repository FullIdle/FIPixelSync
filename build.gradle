plugins {
    id 'java'
}

group = 'org.figsq.fipixelsync.fipixelsync.Main'
version = '2.1-SNAPSHOT'

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        allowInsecureProtocol = true
        url "http://localhost:31647/snapshots"
    }
}

configurations {
    pack
    compileClasspath.extendsFrom pack
    runtimeClasspath.extendsFrom pack
}

dependencies {
    compileOnly 'org.spigotmc:spigot:1.12.2-R0.1-SNAPSHOT'
    compileOnly 'org.spigotmc:spigot-api:1.12.2-R0.1-SNAPSHOT'

    compileOnly 'com.pixelmonmod.pixelmon:Pixelmon:1.12.2-8.4.3'
    compileOnly 'me.forge.forgedev:ForgeDev:1.12.2'

    compileOnly 'me.fullidle.ficore.ficore:FICore:1.3.4-SNAPSHOT'

    pack 'redis.clients:jedis:6.0.0'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.9.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.1'
    compileOnly 'org.projectlombok:lombok:1.18.28'
    annotationProcessor 'org.projectlombok:lombok:1.18.28'
}

java {
    sourceCompatibility = targetCompatibility = 1.8
}

test {
    useJUnitPlatform()
}

processResources {
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand project.properties
    }
}

jar {
    from(configurations.pack.collect {
        it.isDirectory() ? it : zipTree(it)
    })
    exclude 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.register("copy", Copy) {
    from jar.archiveFile
    into 'D:\\Minecraft\\Server\\Test\\Cat\\1.12.2\\3\\plugins'

    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}